STAT.DESCRIBE = LAMBDA(
    data; [type]; [names];
    LET(
        type; WENN(
            WURDEAUSGELASSEN(type); 
            "m"; links(KLEIN(type); 1)
        );
        colids; sequenz(spalten(data));
        vals; zeilenwahl(REDUCE(
            sequenz(1; WENN(type = "o"; 5; 8)); 
            colids;
            LAMBDA(
                lines;
                colid; 
                LET(
                    raw_values; spaltenwahl(data; colid);
                    values; filter(raw_values; istzahl(raw_values)); // drop errors, text and empty cells
                    cnt_values; ANZAHL(values);
                    basic_stats; HSTAPELN(
                            MIN(values);
                            MEDIAN(values);
                            MAX(values);
                            STAT.IQR(values);
                            STAT.MAD(values)
                        );
                    sdev; STABW.S(values);

                    vstapeln(
                        lines; 
                        wenn(
                            type = "o";
                            HSTAPELN(
                                cnt_values;
                                basic_stats
                            );
                            HSTAPELN(
                                cnt_values;
                                MITTELWERT(values);
                                sdev;
                                sdev / WURZEL(cnt_values);
                                basic_stats
                            )
                        )
                    )
                )
            )
        ); colids + 1);

        WENNS(
            WURDEAUSGELASSEN(names); HSTAPELN(colids; vals);
            ZEILEN(names) > 1; HSTAPELN(names; vals);
            WAHR; HSTAPELN(mtrans(names); vals)
        )
    )
);;