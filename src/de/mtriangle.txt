MTRIANGLE=LAMBDA(
    size; [p_lower]; [p_strict];
    LET(
        b_lower; ODER(WURDEAUSGELASSEN(p_lower); p_lower);
        b_strict; ODER(WURDEAUSGELASSEN(p_strict); NICHT(p_strict));
        WENN(
            UND(ANZAHL(size) = 1; ANZAHL2(b_lower) = 1; ANZAHL2(b_strict) = 1); 
            LET(
                vector; SEQUENZ(size); 
                tvector; MTRANS(vector); 
                1 * WENNS(
                    UND(b_lower; b_strict); vector > tvector; 
                    b_lower; vector >= tvector; 
                    b_strict; vector < tvector; 
                    WAHR; vector <= tvector
                )
            );
            #WERT!
        )
    ) 
);;