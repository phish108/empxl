= LAMBDA( 
    p_values; p_groups; [alternative]; [paired]; 
    LET(
        ab_names; EINDEUTIG(p_groups);
        paired; WENN(WURDEAUSGELASSEN(paired); 3; wenn(paired; 1; 3));
        alternative; WENN(WURDEAUSGELASSEN(alternative); "t"; PREP.ALTERNATIVE(alternative));
        altV; wenn(alternative = "t"; 2; 1);
        altO; wenn(alternative = "g"; WAHR; FALSCH);
        g1; ZEILENWAHL(ab_names; 1);
        g2; ZEILENWAHL(ab_names; 2);
        p_v; WENN(
            ANZAHL2(ab_names) <> 2; 
            #KALK!; 
            T.TEST(FILTER(p_values; p_groups = g1); FILTER(p_values; p_groups = g2); altV; paired)
        );
        WENN(altO; 1 - p_v; p_v)
    )
)